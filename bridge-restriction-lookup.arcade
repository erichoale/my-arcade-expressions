// -----------------------------------------------------------------------------
// CONFIGURATION (SAFE TO EDIT)
// Replace these placeholder values with your actual item and layer information.
// -----------------------------------------------------------------------------

var bridgeItemID  = "<ITEM_ID_REDACTED>";     // ArcGIS Online Item ID
var bridgeLayerID = 0;                        // Layer index inside the item

// Portal URL for your ArcGIS Online or Enterprise organization
var arcgisPortal = Portal("https://<your-org>.maps.arcgis.com");


// -----------------------------------------------------------------------------
// DO NOT CHANGE ANYTHING BELOW THIS LINE
// Core logic for retrieving bridge data and summarizing intersecting features.
// -----------------------------------------------------------------------------

// Retrieve the Feature Layer from the portal
var bridgeLayer = FeatureSetByPortalItem(
    arcgisPortal,
    bridgeItemID,
    bridgeLayerID,
    [
        "BRIDGE_NUMBER",
        "Weight_Rest_Truck",
        "Weight_Rest_Semi_Trailer_Combo",
        "Weight_Rest_Truck_Full_Trailer"
    ]
);

// Create a buffer around the current feature (e.g., a proposed line)
var buff = Buffer($feature, 100, "METERS");

// Select bridge features that intersect the buffer
var intersectsLayer = Intersects(bridgeLayer, buff);
var intcount = Count(intersectsLayer);

// String to store formatted output
var expr = "";

// Build output for each intersecting bridge
if (intcount > 0) {
    for (var f in intersectsLayer) {
        var weightTruck       = DefaultValue(f.Weight_Rest_Truck, "N/A");
        var weightSemi        = DefaultValue(f.Weight_Rest_Semi_Trailer_Combo, "N/A");
        var weightFullTrailer = DefaultValue(f.Weight_Rest_Truck_Full_Trailer, "N/A");
        var bridgeName        = DefaultValue(f.BRIDGE_NUMBER, "N/A");
        
        var atribVal =
            "Bridge No.: " + bridgeName +
            " - Truck Wt: " + weightTruck +
            " | Semi Wt: " + weightSemi +
            " | Full Wt: " + weightFullTrailer;

        expr += atribVal + ";";
    }
}

// Return final summary string
return expr;
